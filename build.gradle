buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'

apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/pmd.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/findbugs.gradle'
apply from: 'gradle/jdepend.gradle'
apply from: 'gradle/asciidoctor.gradle'

if (project.hasProperty('env') &&  project.findProperty('env') == 'dev') {
    apply from: 'gradle/profile_dev.gradle'
} else {
    apply from: 'gradle/profile_default.gradle'
}

group = 'hr.mladen.cikara'
version = '0.0.1-SNAPSHOT'

description = "spring-hal-browser-learning-test"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

ext {
    springCloudVersion = 'Greenwich.RELEASE'
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.cloud:spring-cloud-starter-oauth2')
    implementation('org.flywaydb:flyway-core')
    implementation('javax.json:javax.json-api')
    implementation('org.glassfish:javax.json:1.1.3')

    compileOnly('org.projectlombok:lombok')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.1.1')
    testImplementation('org.junit.platform:junit-platform-launcher:1.3.1')
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation('org.assertj:assertj-core')
    testImplementation('org.codehaus.groovy:groovy')
    testImplementation('org.spockframework:spock-spring:1.2-groovy-2.5')

    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}